Ambigous grammar is difficult to parse.
expression     → literal
               | unary
               | binary
               | grouping ;

literal        → NUMBER | STRING | "true" | "false" | "nil" ;
grouping       → "(" expression ")" ;
unary          → ( "-" | "!" ) expression ;
binary         → expression operator expression ;
operator       → "==" | "!=" | "<" | "<=" | ">" | ">="
               | "+"  | "-"  | "*" | "/" ;

---------------------------------------------------------------------------
program        -> declaration* EOF ;
declaration    -> varDecl | statement ;

varDecl        -> "var" IDENTIFIER ( "=" expression )? ";" ;
statement      -> assignment | exprStatement | printStatement | block | ifStatement 
                  | whileStatement | forStatement | breakStatement | continueStatement;

assignment     -> IDENTIFIER "=" expression ";" ;
exprStatement  -> expression ";" ;
printStatement -> "print" expression ";" ;
block          -> "{" declaration* "}" ;
ifStatement    -> "if" "(" expression ")" statement ("else" statement)? ;
whileStatement -> "while" "(" expression ")" statement ;
forStatement   -> "for" "(" (varDecl | exprStatement | ";") expression? ";" expression? ")" statement;
breakStatement -> "break" ";" ;
continueStatement -> "continue" ";" ; 

expression     -> comma ;
comma          -> ternary ( "," ternary )* ;
ternary        -> logic_or ("?" expression ":" ternary )? ;

logic_or       -> logic_and ( "or" logic_and )* ;
logic_and      -> equality ( "and" equality )* ;

equality       -> comparison ( ( "!=" | "==" ) comparison )* ;
comparison     -> term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term           -> factor ( ( "-" | "+" ) factor )* ;
factor         -> unary ( ( "/" | "*" ) unary )* ;
unary          -> ( "!" | "-" ) unary | primary ;
primary        -> NUMBER | STRING | "true" | "false" | "nil" | "(" expression ")" | IDENTIFIER ;
