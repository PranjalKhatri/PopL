program           -> declaration* EOF ;
declaration       -> varDecl | statement | funcDecl;

funcDecl          -> "fun" function;
function          -> IDENTIFIER "(" parameters? ")" block ;
parameters        -> IDENTIFIER ( "," IDENTIFIER ) ;
varDecl           -> "var" IDENTIFIER ( "=" expression )? ";" ;
statement         -> assignment | exprStatement | block | ifStatement 
                     | whileStatement | forStatement | breakStatement | continueStatement | returnStatement ;

assignment        -> IDENTIFIER "=" expression ";" ;
exprStatement     -> expression ";" ;
block             -> "{" declaration* "}" ;
ifStatement       -> "if" "(" expression ")" statement ("else" statement)? ;
whileStatement    -> "while" "(" expression ")" statement ;
forStatement      -> "for" "(" (varDecl | exprStatement | ";") expression? ";" expression? ")" statement;
breakStatement    -> "break" ";" ;
continueStatement -> "continue" ";" ; 
returnStatement   -> "return" expression? ";" ;

expression        -> comma ;
comma             -> ternary ( "," ternary )* ;
ternary           -> logic_or ("?" expression ":" ternary )? ;

logic_or          -> logic_and ( "or" logic_and )* ;
logic_and         -> equality ( "and" equality )* ;

equality          -> comparison ( ( "!=" | "==" ) comparison )* ;
comparison        -> term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term              -> factor ( ( "-" | "+" ) factor )* ;
factor            -> unary ( ( "/" | "*" ) unary )* ;
unary             -> ( "!" | "-" ) unary | call ;
call              -> primary ( "(" arguments? ")" )* ;
arguments         -> expression ( "," expression )* ;
primary           -> NUMBER | STRING | "true" | "false" | "nil" | "(" expression ")" | IDENTIFIER | functionExpr;
functionExpr      -> "fun" "(" parameters? ")" block ;
